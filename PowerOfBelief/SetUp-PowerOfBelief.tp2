BACKUP ~PowerOfBelief/backup~
AUTHOR ~aquadrizzt@gmail.com~
VERSION ~0.0~ 

/* Language Settings */
AUTO_TRA ~PowerOfBelief/tra/%s~
LANGUAGE ~English~ ~english~ ~PowerOfBelief/tra/english/setup.tra~

// ========================================================
// MAIN COMPONENT
// -----------------------------
// Contents:
// - Mourns-for-Trees teaching TNO Priest
// - Grace allowing relearning of Priest 
// - Specialization bonuses for level 7 and level 12 
// - Specialization tattoos available from Fell 
// -----------------------------
// Future content:
// - Nature domain after completing "Believe in Trees" quest for Mourns-for-Trees
// - Experience domain from a quest for Fall-from-Grace
// - Oblivion domain from a quest for Hargrimm 
// - More domains to come... 
// - Items for a priestly TNO 
// ========================================================

BEGIN @0

COMPILE ~PowerOfBelief/data/qd_pob_mft.d~

COPY ~PowerOfBelief/data/qd_f2p.cre~ ~override~
	WRITE_EVALUATED_ASCII 0x248 ~qd_f2p~ 
	WRITE_EVALUATED_ASCII 0x2cc ~qd_f2p~
	
COMPILE ~PowerOfBelief/data/qd_f2p.baf~ 

COPY ~PowerOfBelief/data/qd_p2f.cre~ ~override~
	WRITE_EVALUATED_ASCII 0x248 ~qd_p2f~ 
	WRITE_EVALUATED_ASCII 0x2cc ~qd_p2f~
	
COMPILE ~PowerOfBelief/data/qd_p2f.baf~ 

//BUILDING THE FIGHTER TO PRIEST CONVERSION
<<<<<<<< QD/tno_fighter_level.baf
	IF
		Level(Protagonist,%lvl%)
		!Class(Protagonist,CLERIC)
	THEN	
		RESPONSE #100 
			SetGlobal("qd_tno_fighter_lvl","GLOBAL",%lvl%)
			ChangeStat(Protagonist,XP,-%xp%,ADD)
			Continue() 
	END 
>>>>>>>>

<<<<<<<< QD/tno_fighter_level_max.baf
	IF
		!Class(Protagonist,CLERIC)
		OR(2)
		Level(Protagonist,%lvl%)
		LevelGT(Protagonist,%lvl%)
	THEN	
		RESPONSE #100 
			SetGlobal("qd_tno_fighter_lvl","GLOBAL",%lvl%)
			ChangeStat(Protagonist,XP,-%xp%,ADD)
			Continue() 
	END
>>>>>>>>

<<<<<<<< QD/tno_priest_level_zero.baf
	IF
		Global("qd_tno_priest_lvl","GLOBAL",0)
		!Class(Protagonist,CLERIC)
	THEN	
		RESPONSE #100 
			SetNamelessClass(CLERIC)
			ChangeStat(Protagonist,Level,0,SET)
			ChangeStat(Protagonist,XP,0,SET)
			Continue() 
	END 
>>>>>>>>

<<<<<<<< QD/tno_priest_level.baf
	IF
		Global("qd_tno_priest_lvl","GLOBAL",%lvl%)
		!Class(Protagonist,CLERIC)
	THEN	
		RESPONSE #100 
			SetNamelessClass(CLERIC)
			ChangeStat(Protagonist,Level,%lvl%,SET)
			ChangeStat(Protagonist,XP,%xp%,ADD)
			Continue() 
	END 
>>>>>>>>

<<<<<<<< QD/tno_priest_level_max.baf
	IF
		!Class(Protagonist,CLERIC)
		OR(2)
		Global("qd_tno_priest_lvl","GLOBAL",%lvl%)
		GlobalGT("qd_tno_priest_lvl","GLOBAL",%lvl%)
	THEN	
		RESPONSE #100 
			SetNamelessClass(CLERIC)
			ChangeStat(Protagonist,Level,%lvl%,SET)
			ChangeStat(Protagonist,XP,%xp%,ADD)
			Continue() 
	END
>>>>>>>>

<<<<<<<< QD/tno_priest_conversion_end.baf
	IF
		True()
	THEN
		RESPONSE #100
			DestroySelf()
	END
>>>>>>>>
		
//Spells known progression reading
COPY_EXISTING ~XPLEVEL.2DA~ ~override~
	COUNT_2DA_COLS num_cols
	COUNT_2DA_ROWS num_cols num_rows
	READ_2DA_ENTRIES_NOW ~r2en_xp_level~ num_cols
	FOR (col = 1; col < num_cols; col += 1) BEGIN
		READ_2DA_ENTRY_FORMER ~r2en_xp_level~ 2 col xp
		INNER_ACTION BEGIN 
			ACTION_IF (col = %num_cols%-1) THEN BEGIN 
				EXTEND_BOTTOM ~qd_f2p.bcs~ ~QD/tno_fighter_level_max.baf~ 
					lvl = (%col%)
					xp = (%xp%)
					EVAL 
			END 
			ELSE BEGIN 
				EXTEND_BOTTOM ~qd_f2p.bcs~ ~QD/tno_fighter_level.baf~ 
					lvl = (%col%)
					xp = (%xp%)
					EVAL  
			END 
		END 
	END 
	INNER_ACTION BEGIN 
		EXTEND_BOTTOM ~qd_f2p.bcs~ ~QD/tno_priest_level_zero.baf~
	END 
	FOR (col = 1; col < num_cols; col += 1) BEGIN
		READ_2DA_ENTRY_FORMER ~r2en_xp_level~ 5 col xp
		INNER_ACTION BEGIN 
			ACTION_IF (col = %num_cols%-1) THEN BEGIN 
				EXTEND_BOTTOM ~qd_f2p.bcs~ ~QD/tno_priest_level_max.baf~ 
					lvl = (%col%)
					xp = (%xp%)
					EVAL 
			END 
			ELSE BEGIN 
				EXTEND_BOTTOM ~qd_f2p.bcs~ ~QD/tno_priest_level.baf~ 
					lvl = (%col%)
					xp = (%xp%)
					EVAL  
			END 
		END 
	END
	
EXTEND_BOTTOM ~qd_f2p.bcs~ ~QD/tno_priest_conversion_end.baf~

//BUILDING THE PRIEST TO FIGHTER CONVERSION
<<<<<<<< QD/tno_priest_level_p2f.baf
	IF
		!Class(Protagonist,FIGHTER)
		Level(Protagonist,%lvl%)
	THEN	
		RESPONSE #100 
			SetGlobal("qd_tno_priest_lvl","GLOBAL",%lvl%)
			ChangeStat(Protagonist,XP,-%xp%,ADD)
			Continue() 
	END 
>>>>>>>>

<<<<<<<< QD/tno_priest_level_max_p2f.baf
	IF
		!Class(Protagonist,FIGHTER)
		OR(2)
		Level(Protagonist,%lvl%)
		LevelGT(Protagonist,%lvl%)
	THEN	
		RESPONSE #100 
			SetGlobal("qd_tno_priest_lvl","GLOBAL",%lvl%)
			ChangeStat(Protagonist,XP,-%xp%,ADD)
			Continue() 
	END
>>>>>>>>

<<<<<<<< QD/tno_fighter_level_zero_p2f.baf
	IF
		Global("qd_tno_fighter_lvl","GLOBAL",0)
		!Class(Protagonist,FIGHTER)
	THEN	
		RESPONSE #100 
			SetNamelessClass(FIGHTER)
			ChangeStat(Protagonist,Level,0,SET)
			ChangeStat(Protagonist,XP,0,SET)
			Continue() 
	END 
>>>>>>>>

<<<<<<<< QD/tno_fighter_level_p2f.baf
	IF
		Global("qd_tno_fighter_lvl","GLOBAL",%lvl%)
		!Class(Protagonist,FIGHTER)
	THEN	
		RESPONSE #100 
			SetNamelessClass(FIGHTER)
			ChangeStat(Protagonist,Level,%lvl%,SET)
			ChangeStat(Protagonist,XP,%xp%,ADD)
			Continue() 
	END 
>>>>>>>>

<<<<<<<< QD/tno_fighter_level_max_p2f.baf
	IF
		!Class(Protagonist,FIGHTER)
		OR(2)
		Global("qd_tno_fighter_lvl","GLOBAL",%lvl%)
		GlobalGT("qd_tno_fighter_lvl","GLOBAL",%lvl%)
	THEN	
		RESPONSE #100 
			SetNamelessClass(FIGHTER)
			ChangeStat(Protagonist,Level,%lvl%,SET)
			ChangeStat(Protagonist,XP,%xp%,ADD)
			Continue() 
	END
>>>>>>>>

<<<<<<<< QD/tno_priest_conversion_end.baf
	IF
		True()
	THEN
		RESPONSE #100
			DestroySelf()
	END
>>>>>>>>
		
//Spells known progression reading
COPY_EXISTING ~XPLEVEL.2DA~ ~override~
	COUNT_2DA_COLS num_cols
	COUNT_2DA_ROWS num_cols num_rows
	READ_2DA_ENTRIES_NOW ~r2en_xp_level~ num_cols
	FOR (col = 1; col < num_cols; col += 1) BEGIN
		READ_2DA_ENTRY_FORMER ~r2en_xp_level~ 2 col xp
		INNER_ACTION BEGIN 
			ACTION_IF (col = %num_cols%-1) THEN BEGIN 
				EXTEND_BOTTOM ~qd_p2f.bcs~ ~QD/tno_fighter_level_max_p2f.baf~ 
					lvl = (%col%)
					xp = (%xp%)
					EVAL 
			END 
			ELSE BEGIN 
				EXTEND_BOTTOM ~qd_p2f.bcs~ ~QD/tno_fighter_level_p2f.baf~ 
					lvl = (%col%)
					xp = (%xp%)
					EVAL  
			END 
		END 
	END 
	INNER_ACTION BEGIN 
		EXTEND_BOTTOM ~qd_p2f.bcs~ ~QD/tno_fighter_level_zero_p2f.baf~
	END 
	FOR (col = 1; col < num_cols; col += 1) BEGIN
		READ_2DA_ENTRY_FORMER ~r2en_xp_level~ 5 col xp
		INNER_ACTION BEGIN 
			ACTION_IF (col = %num_cols%-1) THEN BEGIN 
				EXTEND_BOTTOM ~qd_p2f.bcs~ ~QD/tno_priest_level_max_p2f.baf~ 
					lvl = (%col%)
					xp = (%xp%)
					EVAL 
			END 
			ELSE BEGIN 
				EXTEND_BOTTOM ~qd_p2f.bcs~ ~QD/tno_priest_level_p2f.baf~ 
					lvl = (%col%)
					xp = (%xp%)
					EVAL  
			END 
		END 
	END
	
EXTEND_BOTTOM ~qd_p2f.bcs~ ~QD/tno_priest_conversion_end.baf~

//Priest specialization tattoos
COPY ~PowerOfBelief/data/QDTTPR1.itm~ ~override~ 
	SAY NAME1 @10 
	SAY IDENTIFIED_DESC @11 

	
COPY ~PowerOfBelief/data/QDTTPR2.itm~ ~override~ 
	SAY NAME1 @12
	SAY IDENTIFIED_DESC @13 

//adding priest tattoos to fell's parlor
COPY_EXISTING ~fell.sto~ ~override~ 
	ADD_STORE_ITEM ~qdttpr1~ #0 #0 #0 ~IDENTIFIED~ #1 
	ADD_STORE_ITEM ~qdttpr2~ AFTER ~qdttpr1~ #0 #0 #0 ~IDENTIFIED~ #1 
	
//sadness to add triggers for priest tattoo availability and specialization bonuses
<<<<<<<< QD/tno_priest_spec_checks.baf
	//priest first spec
	IF
		Global("Specialist","GLOBAL",0)
		Global("QD_PRIEST_SPEC_7","GLOBAL",0)
		Class(Protagonist,CLERIC)
		LevelGT(Protagonist,6)
	THEN
		RESPONSE #100
			SetGlobal("QD_PRIEST_SPEC_7","GLOBAL",1)
			DisplayStringNoNameHead(Protagonist,34865)
			//priest first spec bonus
			ChangeStat(Protagonist,WIS,1,ADD)
			Continue()
	END
	
	//other -> priest dual spec
	IF
		GlobalGT("Specialist","GLOBAL",0)
		GlobalLT("Specialist","GLOBAL",4)
		Global("QD_PRIEST_SPEC_7","GLOBAL",0)
		Class(Protagonist,CLERIC)
		LevelGT(Protagonist,6)
	THEN
		RESPONSE #100
			SetGlobal("QD_PRIEST_SPEC_7","GLOBAL",1)
			SetGlobal("QD_PRIEST_DUAL_SPEC","GLOBAL",1)
			DisplayStringNoNameHead(Protagonist,34865)
			ChangeStat(Protagonist,WIS,1,ADD)
			Continue()
	END
	
	//priest double spec
	IF
		Global("Specialist","GLOBAL",0)
		Global("QD_PRIEST_SPEC_7","GLOBAL",1)
		Global("QD_PRIEST_DUAL_SPEC","GLOBAL",0)
		Global("QD_PRIEST_SPEC_12","GLOBAL",0)
		Class(Protagonist,CLERIC)
		LevelGT(Protagonist,11)
	THEN
		RESPONSE #100
			SetGlobal("QD_PRIEST_SPEC_12","GLOBAL",1)
			DisplayStringNoNameHead(Protagonist,34865)
			ChangeStat(Protagonist,WIS,2,ADD)
			ChangeStat(Protagonist,CHR,1,ADD)
			ChangeStat(Protagonist,RESISTMAGIC,10,ADD)
			Wait(2)
			DisplayStringNoNameHead(Protagonist,34889)
			
			Continue()
	END
	
	//priest -> other dual spec
	IF
		GlobalGT("Specialist","GLOBAL",0)
		GlobalLT("Specialist","GLOBAL",4)
		Global("QD_PRIEST_SPEC_7","GLOBAL",1)
		Global("QD_PRIEST_DUAL_SPEC","GLOBAL",0)
		Global("QD_PRIEST_SPEC_12","GLOBAL",0)
	THEN
		RESPONSE #100
			SetGlobal("QD_PRIEST_DUAL_SPEC","GLOBAL",1)
			Continue()
	END 
	
	//priest 12 -> fighter 7 [cancels bonuses] 
	IF
		Global("Specialist","GLOBAL",1)
		Global("QD_PRIEST_SPEC_12","GLOBAL",1)
	THEN
		RESPONSE #100
			SetGlobal("Specialist","GLOBAL",0)
			ChangeStat(Protagonist,STR,-1,ADD)
			Continue()
	END 
	
	//priest 12 -> thief 7 [cancels bonuses] 
	IF
		Global("Specialist","GLOBAL",2)
		Global("QD_PRIEST_SPEC_12","GLOBAL",1)
	THEN
		RESPONSE #100
			SetGlobal("Specialist","GLOBAL",0)
			ChangeStat(Protagonist,DEX,-1,ADD)
			Continue()
	END 
	
	//priest 12 -> mage 7 [cancels bonuses] 
	IF
		Global("Specialist","GLOBAL",3)
		Global("QD_PRIEST_SPEC_12","GLOBAL",1)
	THEN
		RESPONSE #100
			SetGlobal("Specialist","GLOBAL",0)
			ChangeStat(Protagonist,INT,-1,ADD)
			Continue()
	END 
	
	//priest 7 -> fighter 12 [cancels bonuses] 
	IF
		Global("Specialist","GLOBAL",4)
		Global("QD_PRIEST_SPEC_7","GLOBAL",1)
	THEN
		RESPONSE #100
			SetGlobal("Specialist","GLOBAL",0)
			ChangeStat(Protagonist,STR,-1,ADD)
			ChangeStat(Protagonist,CON,-1,ADD)
			ChangeStat(Protagonist,MAXHITPOINTS,-3,ADD)
			Continue()
	END 
	
	//priest 7 -> thief 12 [cancels bonuses] 
	IF
		Global("Specialist","GLOBAL",5)
		Global("QD_PRIEST_SPEC_7","GLOBAL",1)
	THEN
		RESPONSE #100
			SetGlobal("Specialist","GLOBAL",0)
			ChangeStat(Protagonist,DEX,-2,ADD)
			ChangeStat(Protagonist,LUCK,-1,ADD)
			Continue()
	END 
	
	//priest 7 -> mage 12 [cancels bonuses] 
	IF
		Global("Specialist","GLOBAL",6)
		Global("QD_PRIEST_SPEC_7","GLOBAL",1)
	THEN
		RESPONSE #100
			SetGlobal("Specialist","GLOBAL",0)
			ChangeStat(Protagonist,INT,-2,ADD)
			ChangeStat(Protagonist,WIS,-1,ADD)
			ChangeStat(Protagonist,LORE,-5,ADD)
			Continue()
	END 
>>>>>>>>
EXTEND_TOP ~baldur.bcs~ ~QD/tno_priest_spec_checks.baf~ 

//Add checks for priest "specializations" for Fell's shop
COPY_EXISTING ~fell.sto~ ~override~
	GET_OFFSET_ARRAY tattoos 0x34 4 0x38 4 0 0 0x58 
	PHP_EACH tattoos AS int => tt_off BEGIN 
		READ_ASCII tt_off itmres
		PATCH_IF (~%itmres%~ STRING_EQUAL_CASE ~qdttpr1~) BEGIN 
			WRITE_LONG (tt_off + 0x1c) RESOLVE_STR_REF(@14) 
		END 
		PATCH_IF (~%itmres%~ STRING_EQUAL_CASE ~qdttpr2~) BEGIN 
			WRITE_LONG (tt_off + 0x1c) RESOLVE_STR_REF(@15)
		END 
	END 